//CSATRAKR JOB (3853,MVS),'RUN CSA REPORT',MSGCLASS=U,
//         CLASS=8,REGION=0M
/*JOBPARM S=*
//*
//SAS      EXEC SAS,COND=(4,LT),REGION=0M
//*
//WORK     DD SPACE=(CYL,250),UNIT=WORK
//SASLIST  DD SYSOUT=*
//SYSIN    DD *
OPTIONS CENTER LINESIZE=80 PAGESIZE=58;

FILENAME VSMLIST ('YOUR.CSATRACKER.OUTPUT'BUFNO=20 DISP=SHR;

PROC FORMAT; PICTURE DOTV 0-1024='000000'
                    1024-1024000='000.0K' (MULT=.009765625)
                           OTHER='000.0M' (MULT=.000009765625);
DATA CSA;
 INFILE VSMLIST;
   INPUT @03 ASID       $CHAR4.       /* ASID                   */
         @08 JOBNAME    $CHAR8.       /* JOBNAME                */
         @17 JOBID      $CHAR8.       /* JOBID                  */
         @26 STATUS     $CHAR2.       /* STATUS                 */
         @29 STRG_TYP   $CHAR1.       /* CSA OR SQA             */
         @31 CSA_AREA   HEX8.         /* GETMAINTED AREA        */
         @40 CSA_LEN    HEX8.         /* GETMAINTED AREA LENGTH */
         @51 STRG_KEY   $CHAR1.       /* STORAGE PROTECT KEY    */
         @53 SUBPOOL    $CHAR4.       /* STORAGE SUBPOOL        */
         @58 ALOCDATE   MMDDYY11.     /* STORAGE SUBPOOL        */
         @69 ALOCTIME   TIME8.        /* STORAGE SUBPOOL        */
         @96 SMFID      $CHAR4.;      /* SYSTEM ID              */
    IF CSA_AREA > 16777215 THEN AREA=' >16MEG '; ELSE AREA=' <16MEG ';
    IF STRG_TYP = 'S' THEN CSA_TYPE = 'SQA'; ELSE CSA_TYPE = 'CSA';
*   IF STATUS = 'OG' THEN JOBNAME = '*ORPHAN*';
PROC SUMMARY NWAY;
  CLASS SMFID JOBNAME JOBID CSA_TYPE SUBPOOL AREA;
  VAR CSA_LEN;
  OUTPUT OUT=CSAOUT SUM= N=NUMREC;
RUN;
TITLE 'CSA SUBPOOL ALLOCATION BY CPU';
PROC REPORT SPACING=1 DATA=CSAOUT;
COLUMN SMFID CSA_TYPE SUBPOOL AREA,CSA_LEN;
DEFINE SMFID    / GROUP  WIDTH=4         '-SID/--';
DEFINE CSA_TYPE / GROUP  WIDTH=4                  ;
DEFINE SUBPOOL  / GROUP  WIDTH=7         'SUBPOOL/--';
DEFINE AREA     / ACROSS                 '-LOCATION-';
DEFINE CSA_LEN  / SUM    FORMAT=DOTV8.   '--';
 BREAK AFTER CSA_TYPE / SUMMARIZE OL SKIP;
 BREAK AFTER SMFID    / SUMMARIZE OL SKIP;
RUN;
TITLE 'CSA SUBPOOL ALLOCATION BY CPU:JOBNAME';
PROC REPORT SPACING=1 DATA=CSAOUT;
COLUMN SMFID JOBNAME CSA_TYPE,(AREA,CSA_LEN) NUMREC;
DEFINE SMFID    / GROUP  WIDTH=4         '-SID/--';
DEFINE JOBNAME  / GROUP  WIDTH=8         'JOBNAME-/--';
DEFINE CSA_TYPE / ACROSS                 '-COMMON STORAGE-';
DEFINE AREA     / ACROSS                 '--';
DEFINE CSA_LEN  / SUM    FORMAT=DOTV8.   '--';
 BREAK AFTER SMFID    / SUMMARIZE OL SKIP;
RUN;
